buildscript {
	ext {
		springBootVersion = '1.5.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'ridesharing-app'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir 'src/integrationTest/java'
		}
		resources.srcDir file('src/integrationTest/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
	testClassesDir = project.sourceSets.integrationTest.output.classesDir
	classpath = project.sourceSets.integrationTest.runtimeClasspath
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-security',
			'org.springframework.security:spring-security-data',
			'javax.annotation:jsr250-api:1.0',
			'org.springframework.boot:spring-boot-starter-data-rest',
			'org.springframework.data:spring-data-rest-hal-browser',
			'org.liquibase:liquibase-core',
			'org.springframework.boot:spring-boot-starter-web',
			'org.springframework.boot:spring-boot-starter-actuator'

	runtime 'org.springframework.boot:spring-boot-devtools',
			'com.h2database:h2',
			'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok'

	testCompile 'junit:junit:4.12'

	integrationTestCompile 'org.assertj:assertj-core:3.0.0',
			'org.springframework.boot:spring-boot-starter-test'
}
